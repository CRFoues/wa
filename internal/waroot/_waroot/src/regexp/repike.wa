// 版权 @2023 凹语言 作者。保留所有权利。

// Rob Pike 最小正则实现
// https://www.cs.princeton.edu/courses/archive/spr09/cos333/beautiful.html

func Match(regexp, text: string) => bool {
	return match(regexp, text)
}

// match reports whether regexp matches anywhere in text.
func match(regexp, text: string) => bool {
	println("match", regexp, text)
	if regexp != "" && regexp[0] == '^' {
		return matchHere(regexp[1:], text)
	}
	for {
		if matchHere(regexp, text) {
			return true
		}
		if text == "" {
			return false
		}
		text = text[1:]
	}
}

// matchHere reports whether regexp matches at beginning of text.
func matchHere(regexp, text: string) => bool {
	println("matchHere#", regexp,":", text)

	switch {
	case regexp == "":
		println("matchHere 1")
		return true
	case regexp == "$":
		println("matchHere 2:", regexp, "='$':", regexp == "$")
		return text == ""
	case len(regexp) >= 2 && regexp[1] == '*':
		println("matchHere 3:", rune(regexp[0]),":", regexp[2:],":", text)
		return matchStar(regexp[0], regexp[2:], text)
	case text != "" && (regexp[0] == '.' || regexp[0] == text[0]):
		println("matchHere 4:",regexp[1:],":", text[1:])
		return matchHere(regexp[1:], text[1:])
	}
	return false
}

// matchStar reports whether c*regexp matches at beginning of text.
func matchStar(c: byte, regexp, text: string) => bool {
	for {
		if matchHere(regexp, text) {
			return true
		}
		if text == "" || (text[0] != c && c != '.') {
			return false
		}
		text = text[1:]
	}
}
